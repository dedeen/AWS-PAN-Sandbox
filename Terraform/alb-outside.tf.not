#  Terraform to create an ALB on the internet side of everything. Targets will be the public interfaces 
#      of the Palo Alto Firewalls. Within the firewalls, NAT and Routing will forward traffic to an interior
#      ALB in the WebServer VPC. From there, traffic will be routed to the web server instances in that VPC. 
#
# create the ALB
resource "aws_lb" "ALB-outside" {
  name                                = "ALB-outside"
  internal                            = false
  load_balancer_type                  = "application"
  enable_cross_zone_load_balancing    = true
  ip_address_type                     = "ipv4"
  security_groups                     = [aws_security_group.SG-Inbound-Web.id]
  subnets                             = [module.vpc["secvpc"].intra_subnets[2],module.vpc["secvpc"].intra_subnets[8]]
                                                                          # subnets: 2:sec-az1-pub, 8:sec-az2-pub
  tags = {
    Owner = "dan-via-terraform"
    Name  = "ALB-outside"
  }
} 

# create the ALB target group for http
resource "aws_lb_target_group" "ALB-outside-TG-Group80" {
  name                    = "ALB-outside-TG-Group80"
  port                    = 80
  protocol                = "HTTP"
  target_type             = "ip"
  vpc_id                  = module.vpc["secvpc"].vpc_id
 
  health_check {
    enabled               = true
    matcher               = 200
    path                  = "/"
    port                  = "traffic-port" 
    protocol              = "HTTP"
    timeout               = 5
    healthy_threshold     = 5
    unhealthy_threshold   = 2
    interval              = 10 
  }
    
  tags = {
    Owner = "dan-via-terraform"
    Name  = "ALB-outside-TG-80"
  }
}
#
# create the ALB target group for https
resource "aws_lb_target_group" "ALB-outside-TG-Group443" {
  name                    = "ALB-outside-TG-Group443"
  port                    = 443
  protocol                = "HTTPS"
  target_type             = "ip"
  vpc_id                  = module.vpc["secvpc"].vpc_id
 
  health_check {
    enabled               = true
    matcher               = 200
    path                  = "/"
    port                  = "traffic-port"
    protocol              = "HTTPS"
    timeout               = 5
    healthy_threshold     = 5
    unhealthy_threshold   = 2
    interval              = 10 
  }
    
  tags = {
    Owner = "dan-via-terraform"
    Name  = "ALB-outside-TG-443"
  }
}
#

# create http/80 listener for the outside ALB
resource "aws_lb_listener" "ALB-outside-Listener-80" {
  load_balancer_arn       = aws_lb.ALB-outside.arn
  port                    = 80
  protocol                = "HTTP"
  
  default_action {
    type                  = "forward"
    target_group_arn      = aws_lb_target_group.ALB-outside-TG-Group80.arn 
  }
}  

# create https/443 listener for the outside ALB
resource "aws_lb_listener" "ALB-outside-Listener-443" {
  load_balancer_arn       = aws_lb.ALB-outside.arn
  port                    = 443
  protocol                = "HTTPS"
  ssl_policy              = "ELBSecurityPolicy-TLS13-1-2-2021-06"
  certificate_arn         = "arn:aws:acm:us-west-2:500112433998:certificate/184ed2a9-df40-40c0-90c0-b5974c00f409"
  
  default_action {
    type                  = "forward"
    target_group_arn      = aws_lb_target_group.ALB-outside-TG-Group443.arn 
  }
}  

# register Palo Alto Firewalls with target groups for ALB-outside
#
resource "aws_lb_target_group_attachment" "target-80-PAVM1" {
  target_group_arn = aws_lb_target_group.ALB-outside-TG-Group80.arn
  target_id        = "10.100.2.10"                 # PA-VM-1
  port             = 80
}

resource "aws_lb_target_group_attachment" "target-80-PAVM2" {
  target_group_arn = aws_lb_target_group.ALB-outside-TG-Group80.arn
  target_id        = "10.100.66.10"                # PA-VM-2
  port             = 80
}

resource "aws_lb_target_group_attachment" "target-443-PAVM1" {
  target_group_arn = aws_lb_target_group.ALB-outside-TG-Group443.arn
  target_id        = "10.100.2.10"                 # PA-VM-1
  port             = 443
}

resource "aws_lb_target_group_attachment" "target-443-PAVM2" {
  target_group_arn = aws_lb_target_group.ALB-outside-TG-Group443.arn
  target_id        = "10.100.66.10"                # PA-VM-2
  port             = 443
}

####
  
  
